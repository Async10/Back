#
# Even Fibonacci numbers
#
# Problem 2
#
# Each new term in the fibonacci sequence is generated by adding the previous
# two terms. By starting with 1 and 2, the first 10 terms will be:
#
# 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...
#
# By considering the terms in the Fibonaccie sequence whose values do not exceed
# four million, find the sum of the even-valued terms.
#

proc LIMIT 4000000 end

proc 2dup # ( a b -- a b a b)
    over over
end

proc 2drop # ( a b -- )
    drop drop
end

proc fetch64 # ( mem -- u64 )
    7 +
    0
    8 lshift over @ + swap 1 - swap
    8 lshift over @ + swap 1 - swap
    8 lshift over @ + swap 1 - swap
    8 lshift over @ + swap 1 - swap
    8 lshift over @ + swap 1 - swap
    8 lshift over @ + swap 1 - swap
    8 lshift over @ + swap 1 - swap
    8 lshift over @ + swap drop
end

proc store64 # ( mem u64 -- )
    2dup 255 and ! 8 rshift swap 1 + swap
    2dup 255 and ! 8 rshift swap 1 + swap
    2dup 255 and ! 8 rshift swap 1 + swap
    2dup 255 and ! 8 rshift swap 1 + swap
    2dup 255 and ! 8 rshift swap 1 + swap
    2dup 255 and ! 8 rshift swap 1 + swap
    2dup 255 and ! 8 rshift swap 1 + swap
         255 and !
end

proc main
    1 2
    begin over LIMIT < while
        over 2 % 0 == if
            over mem fetch64 + mem swap store64
        end

        swap over +
    end

    mem fetch64 .
end

main
